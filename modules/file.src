import "sys";

Open := fun (filepath, mode) {
    self := {};
    self.fd = sys.Open(filepath, mode);
    if self.fd == null {
        return error(sys.Error());
    }

    self.Read = fun (size...) {
        if len(size) == 0 {
            curpos := sys.Tell(self.fd);
            sys.Seek(self.fd, sys.SEEK_END, 0);
            size = sys.Tell(self.fd);
            sys.Seek(self.fd, sys.SEEK_SET, curpos);
        } else {
            size = size[0];
        }

        buf := alloc(size);
        if sz := sys.Read(buf, 1, size, self.fd); sz == -1 {
            return error(sys.Error());
        }
        return buf;
    };

    self.Write = fun (mesg) {
        if sz := sys.Write (mesg, 1, len(mesg), self.fd); sz == -1 {
            return error(sys.Error());
        }
        return true;
    };

    self.Close = fun () {
        if self.fd != null {
            sys.Close(self.fd);
            self.fd = null;
        }
    };

    return self;
};