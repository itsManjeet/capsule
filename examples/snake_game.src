sys := use("sys.mod");

let size = sys.gettermsize();
let screen = alloc(size.columns * size.rows, str);
let gameover = false;
let x = roundoff((size.columns - 1) / 2);
let y = roundoff((size.rows - 3) / 2);
let fruitx = 0;
let fruity = 0;
let score = 0;
let flag = -1;

let update_fruit = fun () {
    fruitx = sys.random(1, size.columns - 1);
    fruity = sys.random(1, size.rows - 3);
};

let putch = fun (x, y, ch) {
    screen[y * size.columns + x] = ch;
};

let putstr = fun (x, y, s) {
    for ch in s {
        putch(x, y, ch);
        x = x + 1;
    }
};

let draw = fun () {
    print("\033[H");
    for row in range(size.rows-2) {
        for col in range(size.columns) {
            if row == 0 or col == 0 or row == size.rows - 3 or col == size.columns - 1 {
                putch(col, row, "#");
            } else if row == y and col == x {
                putch(col, row, "@");
            } else if row == fruity and col == fruitx {
                putch(col, row, "*");
            } else {
                putch(col, row, " ");
            }
        }
    }
    putstr(2, size.rows-2, "Score       : " + str(roundoff(score)));
    putstr(2, size.rows-1, "Coordinates : " + str(x, " ", y) + " Screen : " + str(size));
    print(screen);
};

let input = fun () {
    let key = sys.getkey();
    if key == "a" {
        flag = 1;
    } else if key == "s" {
        flag = 2;
    } else if key == "d" {
        flag = 3;
    } else if key == "w" {
        flag = 4;
    } else if key == "x" {
        gameover = true;
    }
};

let logic = fun () {
    if flag == 1 {
        x = x - 1;
    } else if flag == 2 {
        y = y + 1;
    } else if flag == 3 {
        x = x + 1;
    } else if flag == 4 {
        y = y - 1;
    }

    if x <= 1 or x >= size.columns - 1 or
        y <= 1 or y >= size.rows - 3 {
            gameover = true;
        }
    

    if x == fruitx and y == fruity {
        update_fruit();
        score = score + 10;
    }
    flag = 0;
};

update_fruit();

print("\033[?25l");
for not gameover {
    draw();
    input();
    logic();
}

print("\033[?25h");
println ("Gameover");
